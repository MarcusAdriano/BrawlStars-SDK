plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'idea'
    id 'jacoco'
}

group 'io.github.marcusadriano'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

test {
    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }

    useJUnitPlatform()
}

def jacocoExcludes = [
    'io/github/marcusadriano/brawlstars/model/**'
]

def jacocoRulesExcludes = [
        'io.github.marcusadriano.brawlstars.model.**'
]

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'METHOD'
            excludes = jacocoRulesExcludes
            limit {
                counter = 'BRANCH'
                minimum = 0.9
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        }))
    }
}

check {
    dependsOn jacocoTestCoverageVerification
    dependsOn jacocoTestReport
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes", "-Xjvm-default=compatibility"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'

    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}